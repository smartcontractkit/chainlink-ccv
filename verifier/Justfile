set fallback

import '../tools/lib/utils.justfile'

# Default: show available recipes
default:
    just --list

mock: ensure-mockery
    @echo "Cleaning existing mocks..."
    find . -path "*/mocks/*.go" -delete 2>/dev/null || true
    @echo "Generating mocks with mockery..."
    mockery

# Format all go files
fmt: ensure-golangci-lint
    golangci-lint fmt

# Run golangci-lint
lint fix="": ensure-golangci-lint
    golangci-lint run --output.text.path stdout {{ if fix != "" { "--fix" } else { "" } }}

mod-tidy: ensure-go
    go mod tidy

mod-download: ensure-go
    go mod download

test: ensure-go
    go test -v ./...

test-coverage:
    go test -v -coverprofile=coverage.out ./...

build-rc:
    docker build -f Dockerfile -t verifier:rc .

build-dev:
    docker build -f Dockerfile.dev -t verifier:dev ..

publish-rc registry version:
    docker tag verifier:rc {{registry}}/chainlink-ccv-verifier:{{version}}-rc
    docker push {{registry}}/chainlink-ccv-verifier:{{version}}-rc

clean:
    docker rmi verifier:latest

clean-dev:
    docker rmi verifier:dev
