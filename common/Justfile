set fallback

# Use strict bash so failures stop the recipe
set shell := ["bash", "-eu", "-o", "pipefail", "-c"]

import '../tools/lib/utils.justfile'

# Default: show available recipes
default:
    just --list

# Format all go files
fmt: ensure-golangci-lint
    golangci-lint fmt

# Run golangci-lint
lint fix="": ensure-golangci-lint
    golangci-lint run --output.text.path stdout {{ if fix != "" { "--fix" } else { "" } }}

mod-tidy: ensure-go
    go mod tidy

mod-download: ensure-go
    go mod download

proto: ensure-protoc
    ./proto.sh


proto-check: proto
    @git diff --name-only -- *.pb.go > /tmp/proto_diff_output; \
    if [ -s /tmp/proto_diff_output ]; then \
        echo "Error: Generated protobuf files are out of sync"; \
        echo "Run 'just proto' and commit the changes"; \
        echo "Diff:"; \
        git diff -- *.pb.go; \
        exit 1; \
    else \
        echo "Protobuf files are in sync"; \
    fi

test: ensure-go
    go test -v -race ./...

test-coverage: ensure-go
    go test -v -race -coverprofile=coverage.out ./...
