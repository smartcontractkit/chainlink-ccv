set fallback

# Use strict bash so failures stop the recipe
set shell := ["bash", "-eu", "-o", "pipefail", "-c"]

import '../tools/lib/utils.justfile'

# Default: show available recipes
default:
    just --list

# Format all go files
fmt: ensure-golangci-lint
    golangci-lint fmt

# Run golangci-lint
lint fix="": ensure-golangci-lint
    golangci-lint run --output.text.path stdout {{ if fix != "" { "--fix" } else { "" } }}

mod-tidy: ensure-go
    go mod tidy

mod-download: ensure-go
    go mod download

test: ensure-go
    go test -v -race ./...

test-coverage: ensure-go
    go test -v -race -coverprofile=coverage.out ./...

mock: ensure-mockery
    @echo "Cleaning existing mocks..."
    find . -path "*/executor_mocks/*.go" -delete 2>/dev/null || true
    @echo "Generating mocks with mockery..."
    mockery
