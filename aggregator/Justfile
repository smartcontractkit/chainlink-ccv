set fallback

import '../tools/lib/utils.justfile'

# Default: show available recipes
default:
    just --list

# Format all go files
fmt: ensure-golangci-lint
    golangci-lint fmt

# Run golangci-lint
lint fix="": ensure-golangci-lint
    golangci-lint run --output.text.path stdout {{ if fix != "" { "--fix" } else { "" } }}

mod-tidy: ensure-go
    go mod tidy

mod-download: ensure-go
    go mod download

test: ensure-go
    go test -v -race ./...

test-coverage: ensure-go
    go test -v -race -coverprofile=coverage.out ./...

build-rc:
    docker build -f Dockerfile -t aggregator:rc ..

build-dev:
    docker build -f Dockerfile.dev -t aggregator:dev ..

publish-rc registry version:
    docker tag aggregator:rc {{registry}}/chainlink-ccv-aggregator:{{version}}-rc
    docker push {{registry}}/chainlink-ccv-aggregator:{{version}}-rc

clean:
    docker rmi aggregator:latest

clean-dev:
    docker rmi aggregator:dev
