set fallback

import '../tools/lib/utils.justfile'

# Default: show available recipes
default:
    just --list

build:
    docker build -f Dockerfile -t pricer:latest ..

build-dev:
    docker build -f Dockerfile.dev -t pricer:dev ..

build-rc:
    docker build -f Dockerfile -t pricer:rc ..

clean-dev:
    docker rmi pricer:dev

publish-rc registry version:
    docker tag pricer:rc {{registry}}/chainlink-ccv-pricer:{{version}}-rc
    docker push {{registry}}/chainlink-ccv-pricer:{{version}}-rc


# Run pricer locally. 
# Requires either: 
# - KEYSTORE_DATA (base64) and KEYSTORE_PASSWORD env vars. E.g. export KEYSTORE_DATA=$(base64 -i keystore.json)
# - KMS_KEY_NAME and KMS_PROFILE env variable specifying a KMS key and AWS profile to use. 
run config="config.example.toml": build
    docker run --rm \
        -p 9090:9090 \
        -e KEYSTORE_DATA \
        -e KEYSTORE_PASSWORD \
        -v ~/.aws:/root/.aws \
        -v {{justfile_directory()}}/{{config}}:/config.toml \
        pricer:latest run --config /config.toml

clean:
    docker rmi pricer:latest
